cmake_minimum_required(VERSION 3.16)

project(Mahya VERSION 1.2.3 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(SQLite3 REQUIRED)
find_package(Qt6 6.5 REQUIRED COMPONENTS Quick)

qt_standard_project_setup(REQUIRES 6.5)

if (APPLE)
    # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
    # generated by CMake. This variable contains the .icns file name,
    # without the path.
    set(MACOSX_BUNDLE_ICON_FILE mahya.icns)

    # And the following tells CMake where to find and install the file itself.
    set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/Resources/mahya.icns")
    set_source_files_properties(${app_icon_macos} PROPERTIES
        MACOSX_PACKAGE_LOCATION "Resources")

    qt_add_executable(MahyaApp MACOSX_BUNDLE main.cpp ${app_icon_macos})
else()
    qt_add_executable(MahyaApp main.cpp)
endif()

qt_add_qml_module(MahyaApp
    URI Mahya
    VERSION 1.0
    QML_FILES Main.qml
    SOURCES backend.hpp backend.cpp
)

set_target_properties(MahyaApp PROPERTIES
    MACOSX_BUNDLE_GUI_IDENTIFIER com.example.MahyaApp
    MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION}
    MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
    MACOSX_BUNDLE TRUE
    WIN32_EXECUTABLE TRUE
)

target_link_libraries(MahyaApp
    PRIVATE Qt6::Quick
    PRIVATE SQLite::SQLite3
)

include(GNUInstallDirs)
install(TARGETS MahyaApp
    BUNDLE DESTINATION .
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)
