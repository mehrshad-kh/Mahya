cmake_minimum_required(VERSION 3.16)

project(MahyaProject VERSION 1.2.5 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(SQLiteCpp REQUIRED)
find_package(Qt6 6.5 REQUIRED COMPONENTS Quick)

qt_standard_project_setup(REQUIRES 6.5)

if (APPLE)
    # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
    # generated by CMake. This variable contains the .icns file name,
    # without the path.
    set(MACOSX_BUNDLE_ICON_FILE mahya.icns)

    # And the following tells CMake where to find and install the file itself.
    set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/Resources/mahya.icns")
    set_source_files_properties(${app_icon_macos} PROPERTIES
        MACOSX_PACKAGE_LOCATION "Resources")

    qt_add_executable(Mahya MACOSX_BUNDLE main.cpp ${app_icon_macos})
else()
    qt_add_executable(Mahya main.cpp)
endif()

set(CPP_SOURCES
    backend.hpp
    backend.cpp
    utility.hpp
    utility.cpp
)

set(JS_SOURCES
    ErrorDialogCreation.js
)

set(QML_SOURCES
    ErrorDialog.qml
    Main.qml
)

qt_add_qml_module(Mahya
    URI MahyaProject
    VERSION 1.0
    QML_FILES ${QML_SOURCES} ${JS_SOURCES}
    SOURCES ${CPP_SOURCES}
)

set_target_properties(Mahya PROPERTIES
    MACOSX_BUNDLE_GUI_IDENTIFIER com.example.Mahya
    MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION}
    MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
    MACOSX_BUNDLE TRUE
    WIN32_EXECUTABLE TRUE
)

target_link_libraries(Mahya
    PRIVATE Qt6::Quick
    PRIVATE SQLiteCpp
)

include(GNUInstallDirs)
install(TARGETS Mahya
    BUNDLE DESTINATION .
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)
